plugins {
    id("java")
    id("xyz.jpenilla.run-paper") version "2.2.3"
    id("com.github.johnrengelman.shadow") version "8.1.1"
}

group = 'com.github.sheauoian'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "CodeMC"
        url = uri("https://repo.codemc.io/repository/maven-public/")
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url "https://repo.dmulloy2.net/repository/public/"
    }
    // Mythic API
    maven {
        name "Lumine Releases"
        url "https://mvn.lumine.io/repository/maven-public/"
    }
}

dependencies {
    // PaperMC
    compileOnly("io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT")
    // NBT API
    compileOnly("de.tr7zw:item-nbt-api-plugin:2.12.3")
    // FastBoard
    implementation('fr.mrmicky:fastboard:2.1.0')
    // Protocol Lib
    compileOnly("com.comphenix.protocol:ProtocolLib:5.1.0")
    // Mythic API
    compileOnly("io.lumine:Mythic-Dist:5.6.1")
    // SQLite
    implementation("org.xerial:sqlite-jdbc:3.45.2.0")
}
shadowJar {
    relocate("fr.mrmicky.fastboard", "com.github.sheauoian.sleep.api.fastboard")
}



def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}
tasks {
    runServer {
        minecraftVersion("1.20.4")
    }
}
processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
